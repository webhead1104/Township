import net.kyori.indra.git.RepositoryValueSource
import org.eclipse.jgit.api.Git

plugins {
    id("java")
    id("java-library")
    id("net.kyori.blossom") version "2.2.0"
}

version = project.findProperty("plugin_version") as String? ?: "unknown"

dependencies {
    api(libs.inventoryFramework.api)
    api(libs.inventoryFramework.platform)
    api(libs.guava)
    api(libs.jetbrainsAnnotations)
    api(libs.configurate.gson)
    api(libs.configurate.yaml)
    api(libs.classgraph)
    api(libs.adventure.minimessage)
    api(libs.commonsText)
    api(libs.slf4j)
    api(libs.mongodb)
    api(libs.hikari)
    api(libs.lamp.common)

    testImplementation(libs.junit.jupiter)
    testRuntimeOnly(libs.junit.launcher)
    testImplementation(libs.configurate.gson)
    testImplementation(libs.logback)
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(21))
}

abstract class RepositoryUrlValueSource : RepositoryValueSource.Parameterless<String>() {
    override fun obtain(repository: Git): String? {
        return repository.repository.config.getString("remote", "origin", "url")
    }
}

val gitBranch: Provider<String> = indraGit.branchName().orElse("DEV")

val gitRepositoryUrl: Provider<String> = indraGit.repositoryValue(RepositoryUrlValueSource::class.java).orElse("").map {
    it.replace("git@github.com:", "https://github.com/")
}

val gitCommitAbbrev: Provider<String> = indraGit.commit().map { it?.name?.substring(0, 7) ?: "0".repeat(7) }

val gitVersion: Provider<String> = gitBranch.zip(gitCommitAbbrev) { branch, commit ->
    "git-${branch}-${commit}"
}

sourceSets {
    main {
        blossom {
            javaSources {
                property("version", "${project.version} (${gitVersion.get()})")
                properties.put("isDev", isDevBuild(gitBranch.get(), gitRepositoryUrl.get()).toString())
            }
        }
        java {
            srcDir(file("src/main/java"))
            srcDir(file("src/autogenerated/java"))
        }
    }
}

fun isDevBuild(branch: String, repository: String): Boolean {
    return branch != "master" || repository.equals("https://github.com/Webhead1104/Towncraft", ignoreCase = true).not()
}