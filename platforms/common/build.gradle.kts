import net.kyori.indra.git.RepositoryValueSource
import org.eclipse.jgit.api.Git

plugins {
    id("java")
    id("java-library")
    id("net.kyori.blossom") version "2.2.0"
}

version = project.findProperty("plugin_version") as String? ?: "unknown"

dependencies {
    implApi(libs.inventoryFramework.api)
    implApi(libs.inventoryFramework.platform)
    implApi(libs.guava)
    implApi(libs.jetbrainsAnnotations)
    implApi(libs.configurate.gson)
    implApi(libs.configurate.yaml)
    implApi(libs.classgraph)
    implApi(libs.adventure.minimessage)
    implApi(libs.adventure.plainText)
    implApi(libs.commonsText)
    implApi(libs.slf4j)
    implApi(libs.mongodb)
    implApi(libs.hikari)
    implApi(libs.lamp.common)
    implApi(libs.autoService.annotations)

    testImplementation(project(":platforms:common-test"))
    testImplementation(libs.logback)
    testAnnotationProcessor(libs.autoService.processor)
    testImplementation(libs.junit.jupiter)
    testRuntimeOnly(libs.junit.launcher)
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(21))
}

abstract class RepositoryUrlValueSource : RepositoryValueSource.Parameterless<String>() {
    override fun obtain(repository: Git): String? {
        return repository.repository.config.getString("remote", "origin", "url")
    }
}

val gitBranch: Provider<String> = indraGit.branchName().orElse("DEV")

val gitRepositoryUrl: Provider<String> = indraGit.repositoryValue(RepositoryUrlValueSource::class.java).orElse("").map {
    it.replace("git@github.com:", "https://github.com/")
}

val gitCommitAbbrev: Provider<String> = indraGit.commit().map { it?.name?.substring(0, 7) ?: "0".repeat(7) }

val gitVersion: Provider<String> = gitBranch.zip(gitCommitAbbrev) { branch, commit ->
    "git-${branch}-${commit}"
}

sourceSets {
    main {
        blossom {
            javaSources {
                property("version", "${project.version} (${gitVersion.get()})")
                properties.put("isDev", isDevBuild(gitBranch.get(), gitRepositoryUrl.get()).toString())
            }
        }
        java {
            srcDir(file("src/main/java"))
            srcDir(file("src/autogenerated/java"))
        }
    }
}

fun isDevBuild(branch: String, repository: String): Boolean {
    return branch != "master" || repository.equals("https://github.com/Webhead1104/Towncraft", ignoreCase = true).not()
}