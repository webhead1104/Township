@file:OptIn(ExperimentalTime::class)

import net.kyori.indra.git.RepositoryValueSource
import org.eclipse.jgit.api.Git
import kotlin.time.ExperimentalTime

plugins {
    id("java")
    id("java-library")
    id("net.kyori.blossom") version "2.2.0"
}

version = project.findProperty("plugin_version") as String? ?: "unknown"

dependencies {
    api("me.devnatan:inventory-framework-api:3.7.1")
    api("me.devnatan:inventory-framework-platform:3.7.1")
    api("com.google.guava:guava:33.5.0-jre")
    api("org.jetbrains:annotations:26.0.2-1")
    api("org.spongepowered:configurate-gson:4.2.0-GeyserMC-SNAPSHOT")
    api("org.spongepowered:configurate-yaml:4.2.0-GeyserMC-SNAPSHOT")
    api("io.github.classgraph:classgraph:4.8.184")
    api("net.kyori:adventure-text-minimessage:4.26.1")
    api("org.apache.commons:commons-text:1.15.0")
    api("org.slf4j:slf4j-api:2.1.0-alpha1")
    api("org.mongodb:mongodb-driver-sync:5.6.2")
    api("com.zaxxer:HikariCP:7.0.2")
    api("io.github.revxrsal:lamp.common:4.0.0-rc.14")

    testImplementation("org.junit.jupiter:junit-jupiter:6.0.1")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher:6.0.1")
    testImplementation("org.spongepowered:configurate-gson:4.2.0-GeyserMC-SNAPSHOT")
    testImplementation("ch.qos.logback:logback-classic:1.5.23")
}

abstract class RepositoryUrlValueSource : RepositoryValueSource.Parameterless<String>() {
    override fun obtain(repository: Git): String? {
        return repository.repository.config.getString("remote", "origin", "url")
    }
}

val gitBranch: Provider<String> = indraGit.branchName().orElse("DEV")

val gitRepositoryUrl: Provider<String> = indraGit.repositoryValue(RepositoryUrlValueSource::class.java).orElse("").map {
    it.replace("git@github.com:", "https://github.com/")
}

val gitCommitAbbrev: Provider<String> = indraGit.commit().map { it?.name?.substring(0, 7) ?: "0".repeat(7) }

val gitVersion: Provider<String> = gitBranch.zip(gitCommitAbbrev) { branch, commit ->
    "git-${branch}-${commit}"
}

sourceSets {
    main {
        blossom {
            javaSources {
                property("version", "${project.version} (${gitVersion.get()})")
                property("devVersion", isDevBuild(gitBranch.get(), gitRepositoryUrl.get()).toString())
            }
        }
        java {
            srcDir(file("src/main/java"))
            srcDir(file("src/autogenerated/java"))
        }
    }
}

fun isDevBuild(branch: String, repository: String): Boolean {
    return branch != "master" || repository.equals("https://github.com/Webhead1104/Towncraft", ignoreCase = true).not()
}